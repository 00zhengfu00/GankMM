apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.maning.gankmm"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        resConfigs "zh"
    }

    signingConfigs {//签名信息
        release {
            storeFile file("gank_mm.jks")
            storePassword rootProject.ext.storePassword
            keyAlias rootProject.ext.keyAlias
            keyPassword rootProject.ext.keyPassword
        }
    }


    productFlavors {
        maning {}
        wandoujia {}
        "360" {}
        xiaomi {}
        meizu {}
        yingyongbao {}

        productFlavors.all { flavor ->
            flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
        }

    }

    def File customerproguardFile = file('src/proguard.cfg')
    buildTypes {
        release {
            //混淆
            minifyEnabled true
            //设置压缩对齐
            zipAlignEnabled true
            signingConfig signingConfigs.release
            //移除无用的资源文件
            shrinkResources true
            proguardFiles customerproguardFile.path
            buildConfigField "boolean", "LOG_DEBUG", "false"
            // 自定义输出配置
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为xxx_release_v1.0_wandoujia.apk
                        def fileName = "gankmm_${variant.buildType.name}_v${defaultConfig.versionName}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent + "/${variant.buildType.name}", fileName)
                    }
                }
            }
        }
        debug {
            //混淆
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles customerproguardFile.path
            buildConfigField "boolean", "LOG_DEBUG", "true"
        }

    }

}

repositories{
    flatDir{
        dirs 'libs'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:support-v4:25.2.0'
    compile 'com.android.support:design:25.2.0'
    compile 'com.android.support:recyclerview-v7:25.2.0'
    /*RecycleView的间隔线*/
    compile 'com.yqritc:recyclerview-flexibledivider:1.2.9'
    /*解析Json*/
    compile 'com.google.code.gson:gson:2.7'
    /*网络相关*/
    compile 'com.squareup.okhttp3:okhttp:3.3.1'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    /*图片加载Glide*/
    compile 'com.github.bumptech.glide:glide:3.7.0'
    /*日志*/
    compile 'com.github.zhaokaiqiang.klog:library:1.3.0'
    /*提示库*/
    compile 'com.bigkoo:svprogresshud:1.0.2'
    /*注解*/
    compile 'com.jakewharton:butterknife:7.0.1'
    /*下拉刷新框架*/
    compile 'com.github.Aspsine:SwipeToLoadLayout:v1.0.2'
    /*点赞收藏按钮*/
    compile 'com.github.jd-alexander:LikeButton:0.1.8'
    /*material风格的dialogs*/
    compile 'com.afollestad.material-dialogs:core:0.9.3.0'
    /*解析HTML页面*/
    compile 'org.jsoup:jsoup:1.9.2'
    /*权限库*/
    compile 'com.yanzhenjie:permission:1.0.5'
    /*ViewPager指示器*/
    compile 'com.ogaclejapan.smarttablayout:library:1.6.1@aar'
    /*翻滚广告条TextView*/
    compile 'com.github.maning0303:SwitcherView:v1.0.4'
    /*Umeng统计*/
    compile 'com.umeng.analytics:analytics:latest.integration'
    /*极光推送*/
    compile files('libs/jpush-android-2.1.5.jar')
    /*阿里百川的意见反馈*/
    compile(name:'feedbackSdk', ext:'aar')
    compile files('libs/securityguard-3.1.27.jar')
    compile files('libs/utdid4all-1.0.4.jar')
    compile files('libs/alisdk-ut-5.jar')
    /*菜单*/
    compile 'io.github.yavski:fab-speed-dial:1.0.4'
}
